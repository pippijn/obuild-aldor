-------------------------------------------------------------------------
--
-- cs_rgbtl.as
--
-------------------------------------------------------------------------
#include "charset.as"

macro AS == AutoreducedSet( T, RT );

+++\begin{addescription}{computes a reduced Gröbner basis}
+++
+++The parameter \adcode{T} denotes the domain for the elements to compute a reduced Gröbner basis for. Typically, \adcode{T} is a domain implementing a polynomial ring.
+++
+++\adcode{RT} denotes the reduction with respect to which a Gröbner basis is computed. This reduction has to implement polynomial reduction and \adname[TriangularizationReductionType]{consequences} of \adcode{RT} has to compute S-polynomials. For example, \adtype{PolynomialReductionTools} meets the necessary criteria for \adcode{RT}. 
+++
+++For the description of \adcode{TRANG}, let $A$ denote elements of \adcode{T}. If $A$ denotes the input to a \adname{triangularize} function of \adcode{TRALG} and $B$ its result, the ideals generated by $A$ and $B$ have to be equal.
+++\end{addescription}
ReducedGroebnerBasisTools(
  T  : RankedTriangularizationTType,  
  RT : TriangularizationReductionType T,
  GBALG: GroebnerBasisAlgorithmType T == GroebnerBasisTools( T, RT ),
  TRALG: TriangularizationAlgorithmType( T, RT ) == AutoreducedSetTools( T, RT, BasicSetSortedTools( T, RT ) )
) : with {

    ReducedGroebnerBasisAlgorithmType( T, RT );

} == add {

    -------------------------------------------
    
    triangularize( F: Generator T ): ( AS, Set T ) == {
	( gb: List T, premult: Set T ) := (groebnerBasis$GBALG) F;
	( rgb: AS, premultRed: Set T ) := (triangularize$TRALG) gb;

	if T has with {
	    primitivePart: % -> %;
	} then 
	{
	    local rgbWoContent: AS := empty;
	    for rgbElement in rgb repeat 
	    {
		local pRgbWoContent: Partial AS := insert!( (primitivePart$T) rgbElement, rgbWoContent );
		assert ~ failed? pRgbWoContent;
		rgbWoContent := retract pRgbWoContent;

	    }
	    rgb := rgbWoContent;
	}
	( rgb, union!( premultRed, premult ) );
    }
    
    -------------------------------------------
 
}

